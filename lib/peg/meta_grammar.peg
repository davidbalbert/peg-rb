Grammar         <- Spacing Definition+ EndOfFile

Definition      <- Identifier LEFTARROW InlineRules
InlineRules     <- NamedSequence ( SLASH NamedSequence )*
Expression      <- Sequence ( SLASH Sequence )*
NamedSequence   <- Sequence ( DASHES Identifier )?
Sequence        <- Prefix*
Prefix          <- ( AND / NOT )? Suffix
Suffix          <- Primary ( QUERY / STAR / PLUS )?
Primary         <- Identifier !LEFTARROW -- identifier
                 / OPEN Expression CLOSE -- group
                 / Literal
                 / Class
                 / DOT

Identifier      <- IdentStart IdentCont* Spacing
IdentStart      <- [a-zA-Z_]
IdentCont       <- IdentStart / [0-9]
Literal         <- ['] ( !['] Char  )* ['] Spacing
                 / ["] ( !["] Char  )* ["] Spacing
Class           <- '[' ( !']' Range )* ']' Spacing
Range           <- Char '-' Char -- multiple
                 / Char

Char            <- '\\\\'                -- backslash
                 / '\\\"'                -- doubleQuote
                 / '\\\''                -- singleQuote
                 / '\\['                 -- openSquare
                 / '\\]'                 -- closeSquare
                 / '\\b'                 -- backspace
                 / '\\n'                 -- newline
                 / '\\r'                 -- carriageReturn
                 / '\\t'                 -- tab
                 / '\\u' Hex Hex Hex Hex -- unicode
                 / '\\x' Hex Hex         -- hex
                 / !'\\' .               -- regular

Hex             <- [0-9a-fA-F]
LEFTARROW       <- '<-' Spacing
SLASH           <- '/' Spacing
AND             <- '&' Spacing
NOT             <- '!' Spacing
QUERY           <- '?' Spacing
STAR            <- '*' Spacing
PLUS            <- '+' Spacing
OPEN            <- '(' Spacing
CLOSE           <- ')' Spacing
DOT             <- '.' Spacing
DASHES          <- '--' Spacing
Spacing         <- ( Space / Comment )*
Comment         <- '#' ( !EndOfLine . )* EndOfLine
Space           <- ' ' / '\t' / EndOfLine
EndOfLine       <- '\r\n' / '\n' / '\r'
EndOfFile       <- !.
